
    model {
    
    # Priors
    
    # - Occupancy
    for(j in 1:nsite){
    int.psi[j] ~ dunif(0,1)
    }
    # - eDNA availability
    int.theta ~ dunif(0,1) # Intercepts availability probability
    
    #detection
    int.p ~ dunif(0,1)
    
    
    
    # - Before-after effect
    betaBA1 ~ dnorm(0, 0.1)
    betaBA2 ~ dnorm(0, 0.1)
    
    
    #covs
    beta_lsev~ dnorm(0, 0.1)
    beta_hsev~ dnorm(0, 0.1)
    beta_rain ~ dnorm(0,0.1)
    beta_volume~ dnorm(0,0.1)
    
    #two way interactions
    
    beta_i_rain_hsev~ dnorm(0, 0.1)
    beta_i_rain_lsev ~ dnorm(0, 0.1)
    
    beta_lsev_BA1~ dnorm(0, 0.1)
    beta_lsev_BA2~ dnorm(0, 0.1)
    beta_hsev_BA1~ dnorm(0, 0.1)
    beta_hsev_BA2~ dnorm(0, 0.1)
    beta_rain_BA1~ dnorm(0, 0.1)
    beta_rain_BA2~ dnorm(0, 0.1)

    
    # Ecological process 
    for(i in 1:nvisit){
    for(j in 1:nsite){
    logit(pocc[i, j]) <-
    logit(int.psi[j]) + betaBA1 * I_after1[i, j]  +
    betaBA2 * I_after2[i,j] + 
    beta_hsev*h_sev[i,j]+
    beta_lsev*l_sev[i,j]+
    beta_rain*rain[i,j]+
    
    
    beta_i_rain_hsev*rain[i,j]*h_sev[i,j]+
    beta_i_rain_lsev*rain[i,j]*l_sev[i,j]+
    
    beta_lsev_BA1*l_sev[i,j]* I_after1[i, j]+
    beta_lsev_BA2*l_sev[i,j]* I_after2[i, j]+
    beta_hsev_BA1*h_sev[i,j]* I_after1[i, j]+
    beta_hsev_BA2*h_sev[i,j]* I_after2[i, j]+
    
    beta_rain_BA1*rain[i,j]* I_after1[i, j]+
    beta_rain_BA2*rain[i,j]* I_after2[i, j]
    
    z[i, j] ~ dbern(pocc[i, j])
    
    for (k in 1:nrep){
    # Occurrence in sample j
    a[i,j,k] ~ dbern(mu.a[i,j,k])
    mu.a[i,j,k] <- z[i, j] * theta[i,j,k]
    cloglog(theta[i,j,k]) <- cloglog(int.theta)+beta_volume*volume[i,j,k]
    
    
    # Observation process
    for(l in 1:npcr){
    logit(p[i, j, k, l])<- logit(int.p)
    mu.p[i, j, k, l] <-  a[i,j,k] *p[i, j,k,l]
    sp_data[i, j, k, l] ~ dbern(mu.p[i, j, k,l])
    }
    }
    }
    }
    }
    
